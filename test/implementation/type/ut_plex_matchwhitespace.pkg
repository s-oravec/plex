CREATE OR REPLACE PACKAGE ut_plex_matchWhitespace AS

    Description CONSTANT pete_types.typ_description := 'plex_matchWhitespace';

    PROCEDURE continuousAsSingle(D VARCHAR2 := 'continuous whitespace characters matched as single literal - on one line');
    PROCEDURE continuousAsSingleOnMultiLines(D VARCHAR2 := 'continuous whitespace characters matched as single literal - on multiple lines');

END;
/
CREATE OR REPLACE PACKAGE BODY ut_plex_matchWhitespace AS

    ----------------------------------------------------------------------------
    PROCEDURE continuousAsSingle(D VARCHAR2) IS
        l_Text         VARCHAR2(30) := ' ' || chr(9);
        l_sourceLines  plex.source_lines_type := plex.source_lines_type(l_Text);
        l_match        plex_matchWhiteSpace := plex_matchWhiteSpace();
        l_matchedToken plex_token;
    BEGIN
        pete.set_method_description(D);
        plex_lexer.initialize(l_sourceLines);
        l_matchedToken := l_match.isMatch;
        pete_assert.eq(plex_lexer.tk_WhiteSpace, l_matchedToken.tokenType);
        pete_assert.eq(l_Text, l_matchedToken.text);
        -- EOF is what has left
        pete_assert.this(plex_lexer.eof);
    END;

    ----------------------------------------------------------------------------
    PROCEDURE continuousAsSingleOnMultiLines(D VARCHAR2) IS
        l_Text         VARCHAR2(30) := ' ' || chr(9) || chr(10) || chr(9) || ' ' || chr(10);
        l_sourceLines  plex.source_lines_type := plex.source_lines_type(' ' || chr(9) || chr(10), chr(9) || ' ' || chr(10));
        l_match        plex_matchWhiteSpace := plex_matchWhiteSpace();
        l_matchedToken plex_token;
    BEGIN
        pete.set_method_description(D);
        plex_lexer.initialize(l_sourceLines);
        l_matchedToken := l_match.isMatch;
        pete_assert.eq(plex_lexer.tk_WhiteSpace, l_matchedToken.tokenType);
        pete_assert.eq(l_Text, l_matchedToken.text);
        -- EOF is what has left
        pete_assert.this(plex_lexer.eof);
    END;

END;
/
